import org.treesitter.build.Utils

import java.util.stream.Collectors
import java.util.stream.Stream

plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
}

group = 'io.github.bonede'
version = treeSitterCVersion

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation project(":tree-sitter")
}

test {
    useJUnitPlatform()
}

def libName = "tree-sitter-c"


java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = "MavenCentral"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl =  "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = libName
                url = 'https://github.com/bonede/tree-sitter-ng'
                description = "Next generation Tree Sitter Java binding"
                licenses {
                    license {
                        name = 'MIT'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/bonede/tree-sitter-ng.git'
                    developerConnection = 'scm:git:https://github.com/bonede/tree-sitter-ng.git'
                    url = 'https://github.com/bonede/tree-sitter-ng'
                }
                developers {
                    developer {
                        id = 'bonede'
                        name = 'Wang Liang'
                        email = 'bonede@qq.com'
                    }
                }
            }
        }
    }
}


signing {
    sign configurations.archives
    sign publishing.publications
}
def jniCFileName= "org_treesitter_TreeSitterC.c"

tasks.register('downloadSource') {
    group = "build setup"
    description = "Download parser source"
    def zipUrl = "https://github.com/tree-sitter/tree-sitter-c/archive/refs/tags/v${treeSitterCVersion}.zip"
    def downloadDir = Utils.libDownloadDir(project, libName)
    def zip = Utils.libZipFile(project, libName, treeSitterCVersion)
    def parserCFile = Utils.libParserCFile(project, libName, treeSitterCVersion)
    inputs.files(layout.projectDirectory.file("gradle.properties"))
    outputs.files(parserCFile)
    doLast {
        download.run {
            src zipUrl
            dest zip
            overwrite false
        }
        copy {
            from zipTree(zip)
            into downloadDir
        }
    }

}
tasks.register("buildNative") {
    group = "build"
    description = "Build parser native modules"
    dependsOn "downloadSource", rootProject.bootstrap
    def jniSrcDir = Utils.jniSrcDir(project)
    def outDir = Utils.jniOutDir(project)
    def jniCFile =  Utils.jniCFile(project, jniCFileName)
    def parserCFile = Utils.libParserCFile(project, libName, treeSitterCVersion)
    def libSrcDir = Utils.libSrcDir(project, libName, treeSitterCVersion)
    def jniInclude = Utils.jniIncludeDir(project)
    def targets = Utils.treeSitterTargets(project)
    def outputFiles = Stream.of(targets)
            .map( t -> Utils.jniOutFile(project, t, libName))
            .collect(Collectors.toList())

    outputs.files(outputFiles)
    inputs.files(jniCFile, parserCFile, rootProject.layout.projectDirectory.file("gradle.properties"))
    mkdir(outDir)
    doLast{
        targets.each {target ->
            def jniMdInclude = Utils.jniMdInclude(project, target)
            def outFile = Utils.jniOutFile(project, target, libName)
            exec{
                workingDir jniSrcDir
                commandLine(rootProject.downloadZig.zigExe, "cc",
                        "-g0",
                        "-shared",
                        "-target", target,
                        "-I", libSrcDir,
                        "-I", jniInclude,
                        "-I", jniMdInclude,
                        "-o", outFile,
                        jniCFile,
                        parserCFile
                )
            }
        }
        Utils.removeWindowsDebugFiles(project)
    }

}