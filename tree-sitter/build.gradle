import org.treesitter.build.Utils

import java.util.stream.Collectors
import java.util.stream.Stream

plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
}

group = 'io.github.bonede'
version = treesitterVersion

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation project(':tree-sitter-json')
}

test {
    useJUnitPlatform()
}

def libName = "tree-sitter"

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = "MavenCentral"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl =  "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = libName
                url = 'https://github.com/bonede/tree-sitter-ng'
                description = "Next generation Tree Sitter Java binding"
                licenses {
                    license {
                        name = 'MIT'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/bonede/tree-sitter-ng.git'
                    developerConnection = 'scm:git:https://github.com/bonede/tree-sitter-ng.git'
                    url = 'https://github.com/bonede/tree-sitter-ng'
                }
                developers {
                    developer {
                        id = 'bonede'
                        name = 'Wang Liang'
                        email = 'bonede@qq.com'
                    }
                }
            }
        }
    }
}


signing {
    sign configurations.archives
    sign publishing.publications
}

tasks.register('downloadTreeSitterSource') {
    group = "build setup"
    description = "Download TreeSitter source"
    def zipUrl = """https://github.com/tree-sitter/tree-sitter/archive/refs/tags/v${treesitterVersion}.zip"""
    def treeSitterDir = Utils.libDownloadDir(project, libName)
    def treeSitterZip =  Utils.libZipFile(project, libName, treesitterVersion)
    def treeSitterSrcDir = Utils.libSrcDir(project, libName, treesitterVersion, "lib/src")
    inputs.files(layout.projectDirectory.file("gradle.properties"))
    outputs.dir(treeSitterSrcDir)
    doLast {
        download.run {
            src zipUrl
            dest treeSitterZip
            overwrite false
        }
        copy {
            from zipTree(treeSitterZip)
            into treeSitterDir
        }
    }

}
tasks.register("buildNative") {
    group = "build"
    description = "Download TreeSitter native modules"
    dependsOn "downloadTreeSitterSource", rootProject.bootstrap
    def jniDir = Utils.jniSrcDir(project)
    def jniOutDir = Utils.jniOutDir(project)
    def jniCFile = Utils.jniCFile(project, "org_treesitter_TSParser.c")
    def jniInclude = Utils.jniIncludeDir(project)
    def treeSitterSrcDir = Utils.libSrcDir(project, libName, treesitterVersion, "lib/src")
    def treeSitterInclude = Utils.libSrcDir(project, libName, treesitterVersion, "lib/include")
    def treeSitterCFile = treeSitterSrcDir.file("lib.c")
    def targets = Utils.treeSitterTargets(project)
    def outputFiles = Stream.of(targets)
            .map( t -> Utils.jniOutFile(project, t, libName))
            .collect(Collectors.toList())
    inputs.files(treeSitterCFile, jniCFile,  rootProject.layout.projectDirectory.file("gradle.properties"))
    outputs.files(outputFiles)
    doLast{
        mkdir(jniOutDir)
        targets.each {target ->
            def jniMdInclude = Utils.jniMdInclude(project, target)
            def outFile = Utils.jniOutFile(project, target, libName)
            exec{
                workingDir jniDir
                println "arch $target"
                commandLine(rootProject.downloadZig.zigExe, "cc",
                        "-g0",
                        "-shared",
                        "-target", target,
                        "-I", jniInclude,
                        "-I", jniMdInclude,
                        "-I", treeSitterSrcDir,
                        "-I", treeSitterInclude,
                        "-o", outFile,
                        treeSitterCFile,
                        jniCFile,
                )
            }
        }
        Utils.removeWindowsDebugFiles(project)
    }

}